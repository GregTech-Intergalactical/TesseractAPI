buildscript {
    repositories {
        maven { url 'https://files.minecraftforge.net/maven' }
        jcenter()
        mavenCentral()
        gradlePluginPortal()
        maven {
            name = 'parchment'
            url = 'https://maven.parchmentmc.org' 
        }
    }
    dependencies {
        classpath "com.github.jengelman.gradle.plugins:shadow:5.2.0"
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1+', changing: true
        classpath 'org.parchmentmc:librarian:1.+'
    }
}

plugins {
    id 'maven-publish'
}
apply plugin: 'net.minecraftforge.gradle'
apply plugin: "eclipse"
apply plugin: "com.github.johnrengelman.shadow"
apply plugin: 'java'
apply plugin: 'org.parchmentmc.librarian.forgegradle'
archivesBaseName = 'TesseractAPI'
version = "${minecraft_version}-${mod_version}"
group = "com.github.gregtech-intergalactical"

java.toolchain.languageVersion = JavaLanguageVersion.of(8)
configurations {
    shadow
}
minecraft {
    // The mappings can be changed at any time, and must be in the following format.
    // snapshot_YYYYMMDD   Snapshot are built nightly.
    // stable_#            Stables are built at the discretion of the MCP team.
    // Use non-default mappings at your own risk. they may not always work.
    // Simply re-run your setup task after changing the mappings to update your workspace.
    mappings channel: 'parchment', version: '2021.10.17-1.16.5'   
    // makeObfSourceJar = false // an Srg named sources jar is made by default. uncomment this to disable.

    // accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    // Default run configurations.
    // These can be tweaked, removed, or duplicated as needed.
    runs {
        client {
            workingDirectory project.file('run')

            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'

            mods {
                tesseract {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')

            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'

            mods {
                tesseract {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')

            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'

            args '--mod', 'tesseract', '--all', '--output', '"' + rootProject.file('src/generated/resources/') + '"',
                    '--existing', '"' + sourceSets.main.resources.srcDirs[0] + '"'

            mods {
                tesseract {
                    source sourceSets.main
                }
            }
        }
    }
}



// Deobfuscated jar; development purposes.
//import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

repositories {
    jcenter()
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    archiveBaseName.set(project.archivesBaseName)
    archiveVersion.set("${project.version}")
    archiveClassifier.set('sources')
}

/*task javadocJar(type: Jar, dependsOn: javadoc) {
    from javadoc.destinationDir
    archiveClassifier.set('javadoc')
}*/
shadowJar {
    configurations = [project.configurations.shadow]
    archiveClassifier.set('')
    relocate 'org.apache.commons.collections4', 'tesseract.collections'
}
reobf {
    shadowJar {}
}
dependencies {
    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"
    shadow 'org.apache.commons:commons-collections4:4.4'
    testImplementation('junit:junit:4.11')
}

afterEvaluate { project ->
    project.tasks.publishToMavenLocal {
        onlyIf {
            return rootProject.name == "${modid}"
        }
    }
}

jar {
    zip64 true
    manifest {
        attributes([
                "Specification-Title": project.name,
                "Specification-Vendor": "GregTech Intergalactical",
                "Specification-Version": project.version,
                "Implementation-Title": project.name,
                "Implementation-Version": project.version,
                "Implementation-Vendor": "GregTech Intergalactical",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

artifacts {
    archives jar, shadowJar, sourcesJar//, javadocJar
}
publishing {
    tasks.publish.dependsOn 'build', 'reobfJar'
    publications {
        mavenJava(MavenPublication) {
            //artifactId = archivesBaseName
            artifact shadowJar
        }
    }
    repositories {
        maven {
            url "file:///${project.projectDir}/mcmodsrepo"
        }
    }
    //repositories {
    //    rootProject.configurePublishingRepositories(delegate)
    //}
}

